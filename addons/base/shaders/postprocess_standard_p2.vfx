HEADER
{
    CompileTargets = ( IS_SM_50 && ( PC || VULKAN ) );
    Description = "Standard post processing shader, Pass 2";
    DevShader = true;
}

MODES
{
    Default();
    VrForward();
}

FEATURES
{
}

COMMON
{
    #include "postprocess/shared.hlsl"
}

struct VertexInput
{
    float3 vPositionOs : POSITION < Semantic( PosXyz ); >;
    float2 vTexCoord : TEXCOORD0 < Semantic( LowPrecisionUv ); >;
};

struct PixelInput
{
    float2 vTexCoord : TEXCOORD0;

	// VS only
	#if ( PROGRAM == VFX_PROGRAM_VS )
		float4 vPositionPs		: SV_Position;
	#endif

	// PS only
	#if ( ( PROGRAM == VFX_PROGRAM_PS ) )
		float4 vPositionSs		: SV_ScreenPosition;
	#endif
};

VS
{
    PixelInput MainVs( VertexInput i )
    {
        PixelInput o;
        
        o.vPositionPs = float4(i.vPositionOs.xy, 0.0f, 1.0f);
        o.vTexCoord = i.vTexCoord;
        return o;
    }
}

PS
{
    #include "postprocess/common.hlsl"
    #include "postprocess/functions.hlsl"
    #include "postprocess/falloff.hlsl"
    #include "common/proceedural.hlsl"

    RenderState( DepthWriteEnable, false );
    RenderState( DepthEnable, false );

    CreateTexture2D( g_tColorBuffer ) < Attribute( "ColorBuffer" );  	SrgbRead( true ); Filter( MIN_MAG_LINEAR_MIP_POINT ); AddressU( MIRROR ); AddressV( MIRROR ); >;
    CreateTexture2D( g_tDepthBuffer ) < Attribute( "DepthBuffer" ); 	SrgbRead( false ); Filter( MIN_MAG_MIP_POINT ); AddressU( CLAMP ); AddressV( CLAMP ); >;
    SamplerState g_sBilinearWrap < Filter( BILINEAR ); AddressU( WRAP ); AddressV( WRAP ); >;

    struct PixelOutput
    {
        float4 vColor : SV_Target0;
    };

    DynamicCombo( D_BLUR, 0..1, Sys( PC ) );

    //#if D_BLUR
        float flBlurSize< Attribute("standard.blur.size"); Default(0.0f); >;
        float flBlurFalloffModifier< Attribute("standard.blur.falloff"); Default(0.0f); >; 
    //#endif
	
    float3 FetchSceneColor( float2 vScreenUv )
    {
       return Tex2D( g_tColorBuffer, vScreenUv.xy ).rgb;
    }

    float3 GaussianBlurEx( float3 vColor, float2 vTexCoords )
    {
        float flDepth = ReconstructWorldDepth( Tex2DLevel( g_tDepthBuffer, vTexCoords, 0.0 ).r );
        
        float flRemappedBlurSize = MapFalloff( flBlurFalloffModifier, flDepth, 0.0f, flBlurSize );

        float fl2PI = 6.28318530718f;
        float flDirections = 16.0f;
        float flQuality = 4.0f;
        float flTaps = 1.0f;

        [unroll]
        for( float d=0.0; d<fl2PI; d+=fl2PI/flDirections)
        {
            [unroll]
            for(float j=1.0/flQuality; j<=1.0; j+=1.0/flQuality)
            {
                flTaps += 1;
                vColor += FetchSceneColor( vTexCoords + float2( cos(d), sin(d) ) * lerp(0.0f, 0.02, flRemappedBlurSize) * j );    
            }
        }
        return vColor / flTaps;
    }


    PixelOutput MainPs( PixelInput i )
    {
        PixelOutput o;

        float2 vScreenUv = i.vPositionSs.xy / g_vRenderTargetSize;

        float3 vFinalColor = FetchSceneColor( vScreenUv ).rgb;

        #if D_BLUR
            vFinalColor = GaussianBlurEx( vFinalColor, vScreenUv );
        #endif

        o.vColor.rgb = vFinalColor;
        o.vColor.a = 1.0f;
        return o;
    }
}
